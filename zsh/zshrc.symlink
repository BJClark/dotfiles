# Main zshrc configuration file

# Path configuration
# Add Homebrew to PATH
if [ -d "/opt/homebrew/bin" ]; then
  # Apple Silicon
  export PATH="/opt/homebrew/bin:/opt/homebrew/sbin:/opt/homebrew/opt/spaceship/bin:$PATH"
elif [ -d "/usr/local/bin" ]; then
  # Intel
  export PATH="/usr/local/bin:/usr/local/sbin:$PATH"
fi

# Add user bin and other paths
export PATH="$HOME/bin:$PATH"

# Dotfiles directory shortcut - try both possible locations
if [ -d "$HOME/Developer/scidept/dotfiles" ]; then
  export DOTS=$HOME/Developer/scidept/dotfiles
elif [ -d "$HOME/Developer/dotfiles" ]; then
  export DOTS=$HOME/Developer/dotfiles
else
  echo "Warning: Could not find dotfiles directory"
fi

# Projects directory
export PROJECTS=$HOME/Developer

# Load local zshrc config if it exists
if [ -f ~/.zshrc.local ]; then
   source ~/.zshrc.local
fi

# Set terminal colors
export TERM="xterm-256color"

# Configure Ghostty if present
if [[ "$TERM_PROGRAM" == "Ghostty" ]]; then
  export TERM="ghostty"
fi

# Load all zsh files from the dotfiles
typeset -U config_files
config_files=($DOTS/zsh/*.zsh)

# Load path files first
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load configurations - everything but path/completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize completion
autoload -U compinit
compinit

# Load completion files last
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history search with arrow keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Use more readable colors
export LSCOLORS="exfxcxdxbxegedabagacad"
export CLICOLOR=true

# Make sure bat uses a nice theme
export BAT_THEME="Dracula"

# Default editor
export EDITOR="cursor"

# Clean up deuplicate path entries
typeset -U PATH
